#!/opt/perl/bin/perl

use 5.032;

use strict;
use warnings;
no  warnings 'syntax';
use autodie;

use experimental 'signatures';
use experimental 'lexical_subs';

my $repo = "perlweeklychallenge-club";
my $user = $ENV {USER};
my $dir  = $ENV {HOME} . "/GitHub/Programming_Challenges/$repo";

chdir $dir;
chomp (my @list = grep {!m{/perl/}} `find challenge-*/$user/*/ch-[12].*`);

use List::Util qw [sum];

my @guest;
my %languages;

my %map  =  (
    awk       =>  '(GNU) AWK',
    befunge   =>  'Befunge-93',
    node      =>  'Node.js',
    ook       =>  'Ook!',
    sql       =>  'SQL',
);

#
# Collect statistics
#
foreach my $file (@list) {
    $file =~ m {challenge-(?<week>[0-9]{3})/
                $user/
                (?<language>[^/]+)/
                ch-(?<part>[12])\.
                (?<ext>[a-z0-9]+)}x or die "Cannot parse $file";
    next if $+ {language} eq 'sql' && $+ {ext} eq 'table';
    $guest [$+ {week}] [$+ {part}] {$+ {language}} ++;
    $languages {$+ {language}} ++;
}

#
# Print a grid of all solutions in the guest languages.
#
my @all_languages = sort {$a cmp $b} keys %languages;

my $header = << '--';
                  A B B B B B C C C C F F G J L M N O P P R R S T
                  W a a c e r   h o s o o o a u u o o a y   u Q c
                  K s s   f a   e b h r r   v a m d k s t   b L l
                    h i   u i   f o   t t   a   p e ! c h   y
                      c   n n     l   h r       s .   a o
                          g f           a         j   l n
                          e u           n         s
                            c
                            k
--

print $header;

for my $week (keys @guest) {
    next unless $guest [$week];
    my $header = sprintf "Week %3d, part ", $week;
    for my $part (1, 2) {
        next unless $guest [$week] [$part];
        printf "%15s", $header;
        $header  = " " x 9;
        $header .= "part ";
        print $part, " ";
        my $count = 0;
        foreach my $language (@all_languages) {
            printf " %s", $guest [$week] [$part] {$language} ? "X" : " ";
            $count ++ if  $guest [$week] [$part] {$language};
        }
        printf "  %2d\n", $count;
    }
}

print "\n";
print $header;

#
# Counts by language
#
print "\n\n";
my @l = sort {$languages {$b} <=> $languages {$a} ||
                          $a  cmp             $b} keys %languages;

printf "%3d  %s\n" => $languages {$_}, $map {$_} || ucfirst $_ for @l;
printf "%3d  Total\n", sum values %languages;

